package io.github.aasaru.droolsoopath.section05.step1

unit RuleUnitSection05;

import io.github.aasaru.drools.domain.Passport;
import io.github.aasaru.drools.domain.Validation;
import io.github.aasaru.drools.domain.VisaApplication;

import java.time.LocalDate;

rule "Invalidate visa application with invalid passport"
  dialect "java"
  when
    $passport: /passports[ validation == Validation.FAILED ]
    $visaApplication: /visaApplications[ passportNumber == $passport.passportNumber ]
  then
    System.out.println( "Set " + $visaApplication + " invalid as " + $passport + " hasn't passed validation");
    modify($visaApplication) {
      setValidation( Validation.FAILED )
    }
end

rule "Set application without validation info as passed"
  dialect "java"
  when
    $visaApplication: /visaApplications[ validation == Validation.UNKNOWN ]
  then
    System.out.println( $visaApplication + " is without validation info, consider OK for now" );
    modify($visaApplication) {
      setValidation( Validation.PASSED )
    }
end
