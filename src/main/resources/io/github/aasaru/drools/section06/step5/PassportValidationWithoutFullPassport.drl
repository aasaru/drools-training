package io.github.aasaru.drools.section06.step5
 
import io.github.aasaru.drools.domain.Passport;
import io.github.aasaru.drools.domain.Validation;

rule "Valid passport"
  dialect "java"
  agenda-group "validate-passport"
  salience 10
  when
    $passport: Passport( validation == Validation.UNKNOWN )
  then
    System.out.println( $passport + " is without validation info, consider OK for now" );
    modify($passport) {
      setValidation( Validation.PASSED )
    }
end

rule "Expired passport"
  dialect "java"
  agenda-group "validate-passport"
  salience 20
  when
    $passport: Passport( isExpired()  )
  then
    System.out.println( $passport + " is INVALID because it is expired" );
    modify($passport) {
      setValidation( Validation.FAILED ),
      setCause( "passport is expired" )
    }
end

rule "Full passport"
  dialect "java"
  agenda-group "validate-passport"
  salience 20
  date-expires "01-Sep-2018"
  when
    $passport: Passport( unusedVisaPages == 0   )
  then
    System.out.println( $passport + " is INVALID because it has no space for visa" );
    modify($passport) {
      setValidation( Validation.FAILED ),
      setCause( "passport has no space for visa" )
    }
end
