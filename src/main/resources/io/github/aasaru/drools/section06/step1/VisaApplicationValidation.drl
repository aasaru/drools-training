package io.github.aasaru.drools.section06.step1

import io.github.aasaru.drools.domain.Passport;
import io.github.aasaru.drools.domain.Validation;
import io.github.aasaru.drools.domain.VisaApplication;

rule "Invalidate visa application with invalid passport"
  dialect "java"
  when
    $passport: Passport( validation == Validation.FAILED )
    $visaApplication: VisaApplication( passportNumber == $passport.passportNumber,
                                       validation != Validation.FAILED )
  then
    System.out.println( "Set " + $visaApplication + " invalid as " + $passport + " hasn't passed validation");
    modify($visaApplication) {
      setValidation( Validation.FAILED )
    }
end

rule "Set application without validation info as passed"
  dialect "java"
  when
    $visaApplication: VisaApplication( validation == Validation.UNKNOWN )
  then
    System.out.println( $visaApplication + " is without validation info, consider OK for now" );
    modify($visaApplication) {
      setValidation( Validation.PASSED )
    }
end

rule "Invalidate visa application where passport expires earlier than 6 months after visit end date"
  dialect "java"
  when
    $passport: Passport( validation != Validation.FAILED )
    $visaApplication: VisaApplication( passportNumber == $passport.passportNumber,
                                       $passport.expiresOn.isBefore(visitEndDate.plusMonths(6)),
                                       validation != Validation.FAILED )
  then
    System.out.println( "Set " + $visaApplication + " invalid as " + $passport + " not valid 6 months after visit");
    modify($visaApplication) {
      setValidation( Validation.FAILED )
    }
end
