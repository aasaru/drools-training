package io.github.aasaru.drools.section07.step3

import io.github.aasaru.drools.domain.InvalidPassport;
import io.github.aasaru.drools.domain.InvalidVisaApplication;
import io.github.aasaru.drools.domain.Passport;
import io.github.aasaru.drools.domain.Validation;
import io.github.aasaru.drools.domain.ValidVisaApplication;
import io.github.aasaru.drools.domain.VisaApplication;

rule "Invalidate visa application with invalid passport"
  dialect "java"
  when
    $invalidPassport: InvalidPassport()
    $visaApplication: VisaApplication( $invalidPassport.passport.passportNumber == this.passportNumber)
    not ( InvalidVisaApplication( this.visaApplication == $visaApplication ) )
  then
    System.out.println( "Set " + $visaApplication + " invalid as " + $invalidPassport.getPassport() + " is invalid");
    insert( new InvalidVisaApplication($visaApplication) );
end

rule "Set application without validation info as passed"
  dialect "java"
  when
    $visaApplication: VisaApplication()
	  not ( InvalidVisaApplication( this.visaApplication == $visaApplication ) )
  then
	  System.out.println( $visaApplication + " is not known to be invalid, consider Valid" );
	  insertLogical( new ValidVisaApplication($visaApplication) );
end

rule "Invalidate visa application where passport expires earlier than 6 months after visit end date"
  dialect "java"
  when
	  $passport: Passport()
	  not( InvalidPassport( this.passport == $passport) )
	  $visaApplication: VisaApplication( passportNumber == $passport.passportNumber,
								  	                   $passport.expiresOn.isBefore(visitEndDate.plusMonths(6)) )
		not ( InvalidVisaApplication( this.visaApplication == $visaApplication ) )
  then
    System.out.println( "Set " + $visaApplication + " invalid as " + $passport + " is not valid 6 months after planned visit end date");
    insert( new InvalidVisaApplication($visaApplication) );
end
